mode_setup;
if unknown slant: slant:=0 else: currenttransform:=
 identity slanted slant yscaled aspect_ratio fi;

ygap#:=(ht#/13.5u#)*xgap#;              % ajustement vertical
ho#:=o#;                                % horizontal overshoot
leftstemloc#:=2.5u#+s#;                 % position of left stem
barheight#:=.5ht#;                     % height of bar lines
travers#:=1.5u#;
py#:=1px#;                             % vertical pen thickness

grid:=1; % 0=cacher la grille; 1=afficher la grille

define_pixels(s,u);
define_whole_pixels(xgap);
define_whole_vertical_pixels(ygap);
define_blacker_pixels(px,py);
pickup pencircle scaled px;
spirou_pen:=savepen;
define_good_x_pixels(leftstemloc);
define_good_y_pixels(barheight);
define_corrected_pixels(o);
define_horizontal_corrected_pixels(ho);

font_coding_scheme:="MAJUSCULES seulement";
font_slant:=slant;
font_normal_space:=6u#+2s#;
font_normal_stretch:=3u#;
font_normal_shrink:=2u#;
font_quad:=18u#+2s#;
font_extra_space:=2u#;

def beginspirouchar(expr code, unit_width) =
if grid=1:
  makegrid (0,1u,2u,3u,4u,5u,6u)(-2u,1u,2u,3u,4u,5u,6u,7u);
fi
 beginchar(code,unit_width*u#*echel_width,ht#,2u#);
 pickup spirou_pen;
enddef;

def spirou_half(suffix i,j,k,val) =
 cutdraw z.i{0u,y.j-y.i}
 ... (val[x.j,x.i],val[y.i,y.j]){z.j-z.i}
 ... z.j{x.k-x.i,0u}
 ... (val[x.j,x.k],val[y.k,y.j]){z.k-z.j}
 ... z.k{0u,y.k-y.j} enddef;

def spirou_crescent(suffix i,j,k,val) =
 cutdraw z.i{x.j-x.i,0u}
 ... (val[x.i,x.j],val[y.j,y.i]){z.j-z.i}
 ... z.j{0u,y.k-y.i}
 ... (val[x.k,x.j],val[y.j,y.k]){z.k-z.j}
 ... z.k{x.k-x.j,0u} enddef;

def spirou_soft(suffix i,j,val) =
  if x.i<x.j:
    cutdraw z.i{val.u,x.j-x.i}
    ... z.j
  fi
  if x.i>x.j:
   cutdraw z.i{-val.u,x.i-x.j}
   ... z.j
  fi
enddef;

def pen_form(expr type_pen) =
  pickup type_pen scaled px;
enddef;

beginspirouchar("A",6);
x1=.5u; y4=y1=0u;
x2=2.65u; top y2=h-o;
x3=3.35u; top y3=h-o;
x4=5.5u;
x5=1u; y6=y5=1.5u;;
x6=w-1u;
spirou_soft(1,2,.7);
spirou_soft(4,3,.7);
cutdraw z5--z6;
pen_form(pensquare);
draw z2--z3;
labels(1,2,3,4,5,6); endchar;

beginspirouchar("D",6);
x1=x4=.5w;
top y1=h+o;
bot y5=-o;
bot y4=-o;
x5=x2=1u; x3= w-0.75u;
top y2=h+o;
y3=barheight;
spirou_crescent(1,3,4,.85);
pen_form(pensquare)
draw z1--z2--z5--z4;
labels(1,2,3,4,5); endchar;

beginspirouchar("P",6); % letter P
x1=x4=.5w;
top y1=h+o;
y5=.5w;
y4=.5w;
x5=x2=1u; x3= w-0.75u;
top y2=h+o;
y3=4.75u;
x6=1u; y6=-o;
spirou_crescent(1,3,4,.8);
pen_form(pensquare);
draw z1--z2--z5--z4;
cutdraw z5--z6;
labels(1,2,3,4,5,6); endchar;

beginspirouchar("R",6); % letter R
x1=x4=.5w;
top y1=h+o;
y5=.5w;
y7=y4=.5w;
x5=x2=1u; x3= w-0.75u;
top y2=h+o;
y3=4.75u;
x6=1u; y6=-o;
x7=.4w;
x8=.9w; bot y8=-.5u;
spirou_crescent(1,3,4,.8);
cutdraw z7--z8;
pen_form(pensquare);
cutdraw z1--z2--z5--z4;
cutdraw z5--z6;
labels(1,2,3,4,5,6,7,8); endchar;

beginspirouchar("S",6);
x1=5u; y1=4.75u;
x2=x7=.5w; top y2=h+o;
x3=1u;y3=5u;
x4=1.75u;y4=4u;
x5=4.5u;y5=2.5u;
x6=w-0.75u; y6=1.75u;
bot y7=-o;
x8=.75u; y8=2u;
draw z3..z4---z5..z6;
spirou_half(1,2,3,.85);
spirou_half(6,7,8,.85);
labels(1,2,3,4,5,6,7,8); endchar;

beginspirouchar("O",6);
x1=x4=.5w;
top y1=h+o;
bot y4=-o;
x2=.75u; x3= w-0.75u;
y2=y3=barheight;
spirou_half(2,1,3,.88);
spirou_half(2,4,3,.88);
labels(1,2,3,4); endchar;

beginspirouchar("I",3);%letter I
x2=x1=.5w; y1=0;
y2=h;
cutdraw z1--z2;
labels(1,2); endchar;

beginspirouchar("E",6);%letter I
x5=x2=x1=1u; bot y1=-o;
top y2=h+o;
x4=x3=5u;top y3=h+o;
bot y4=-o;
y6=y5=.5h;
x6=4u;
pen_form(pensquare);
path chem;
chem:= z4--z1--z2--z3;
cutdraw chem slanted .45;
cutdraw z5--z6;
labels(1,2,3,4,5,6); endchar;
